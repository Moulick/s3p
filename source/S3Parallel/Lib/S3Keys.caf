import &StandardImport

{}
  supportedKeyChars =
    "" \_!"#$%&'()*+,-./0123456789:;<=>?@ABCDEFGHIJKLMNOPQRSTUVWXYZ[\\]^_`abcdefghijklmnopqrstuvwxyz{|}~

  maxKeyLength = 1024
  lastKeyChar = peek supportedKeyChars
  middleKeyChar = supportedKeyChars[supportedKeyChars.length / 2 | 0]

  getKeyCharIndex = (character) -> if character then supportedKeyChars.indexOf character else 0

  getUpToNextSlash = (key, i) ->
    m =
      key.slice i, key.length
      .match /(^[^\/]*)\//
    if found = m?[0] then key.slice 0, i + found.length

  ## getBisectKey
    IN:
      startAfter:   <String>
      stopAt:       <String>
      bisectPrefix: boolean [default: false]
        if false: find the key alphanumerically between startAfter and stopAT
        if true:  chop off part of startKey and then increment it alpha numerically such that
          the returned key is still > startKey
          This can be an effective way to bisect directory structures:

          startAfter:   /this/is/a/nice/directory/structure
          bisectedKey:  /this/is/b

          Note: This feature is not used by default; it was tested to see if it would enhance performance
            on existing workloads. It did not. However, other directory structures may perform better
            using bisectPrefix.

          Note: it occurs to me that there might be a bug because bisectKey could be after stopAt.

    OUT: bisectedKey:
      if stopAt <= startAfter,
        NULL
      else
        bisectedKey <String>
        Guarantees:
          startAfter < bisectedKey
          bisectedKey < stopAt
  getBisectKey = (startAfter, stopAt, bisectPrefix) -> if startAfter < stopAt
    i = 0
    lastCommonSlash = null
    while startAfter[i] == stopAt[i]
      if startAfter[i] == "/"
        lastCommonSlash = i
      i++

    charIndex1 = if i ==  startAfter.length then 0 else getKeyCharIndex startAfter[i]
    charIndex2 = getKeyCharIndex stopAt[i]

    if charIndex1 < 0 || charIndex2 < 0
      throw new Error "Invalid character found in inputs:\n" + formattedInspect {} startAfter, stopAt, i, charIndex1, charIndex2, supportedKeyChars

    bisectKey =
      switch
      when bisectPrefix &&
          i < prefixBisectI = (i + startAfter.length) / 2 | 0
          && key = getUpToNextSlash startAfter, if lastCommonSlash? then lastCommonSlash + 1 else 0

        # log bisectPrefix: startAfter, stopAt, i, charIndex1, charIndex2, prefixBisectI, nextKeyChar
        getLastKeyWithPrefix key

      when charIndex1 + 1 == charIndex2
        # log one-different: startAfter, stopAt, i, charIndex1, charIndex2
        startAfter.slice 0, i + 1
        + supportedKeyChars[(supportedKeyChars.length - 1 +  getKeyCharIndex startAfter[i + 1]) / 2 | 0]

      else
        # log more-than-one-different: startAfter, stopAt, i, charIndex1, charIndex2
        startAfter.slice 0, i
        + supportedKeyChars[(charIndex1 + charIndex2) / 2 | 0]

    unless startAfter <= bisectKey && bisectKey <= stopAt
      throw new Error "Whoops! #{startAfter} <= #{bisectKey} <= #{stopAt} -- something's not right"

    bisectKey


  escapeKey = (key) ->
    if /[\s()]/.test key
      "" "#{key}"

    else key

  padKey = (key) ->
    pad key, 20 * Math.ceil key.length / 20

  ## getLastKeyWithPrefix
    IN: prefix: string
    OUT:
      returned-key: string
      returned-key has the same prefix as the one passed in
      any possible other-key > returned-key will not have the same prefix
      as the one passed in
  getLastKeyWithPrefix = (prefix) ->
    if prefix.length < maxKeyLength
      prefix + lastKeyChar.repeat maxKeyLength - prefix.length

  debugKey = (key, shouldPad = true) ->
    if key?
      tail = 0
      i = lastIndex = key.length - 1
      while key[i] == lastKeyChar do i--

      key =
        switch
        when key == ''
          "''"

        when i < lastIndex
          escapeKey key.slice 0, i + 1
          + "(#{lastKeyChar}*#{lastIndex - i})"

        else
          escapeKey key

      if shouldPad
        padKey key

      else key

    else "(#{'' + key})"
